/**
 * $LastChangedBy: e104090 
 * $LastChangedDate: 2-13-2015
 *
 * This view pulls all immunizations for a student.
 */

BEGIN TRAN
IF NOT EXISTS (SELECT * FROM sys.views WHERE object_id = OBJECT_ID(N'[APS].[StudentImmunizations]'))
	EXEC ('CREATE VIEW APS.StudentImmunizations AS SELECT 0 AS DUMMY')
GO

ALTER VIEW APS.StudentImmunizations AS

--This script pivots each student’s immunization dosages and groups them by immunization type.

SELECT

	-- SIS_NUMBER
     [VAC_DOSAGES].[VACCINATION_NAME]
      ,[VAC_DOSAGES].[EXEMPT]
      ,[VAC_DOSAGES].[EXEMPT REASON]
      ,[VAC_DOSAGES].[EXEMPT_GRANTED]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 1 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_1]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 2 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_2]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 3 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_3]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 4 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_4]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 5 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_5]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 6 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_6]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 7 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_7]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 8 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_8]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 9 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_9]
      ,MAX(CASE WHEN [VAC_DOSAGES].[RN] = 10 THEN [VAC_DOSAGES].[IMMUNIZATION_DATE] END) AS [DOSAGE_10]
      ,[VAC_DOSAGES].[STUDENT_GU]
	  ,[STU_IMM_VACCINE_GU]
FROM
      (
      SELECT
            [VACCINE].[STUDENT_GU]
			,VACCINE.[STU_IMM_VACCINE_GU]
            ,[DOSAGE].[IMMUNIZATION_DATE]
            ,[VACCINATION_DEF].[VACCINATION_NAME]
            ,[VACCINE].[EXEMPT]
            ,[EXEMPTIONS].[VALUE_DESCRIPTION] AS [EXEMPT REASON]
            ,[VACCINE].[EXEMPT_GRANTED]
            ,ROW_NUMBER() OVER (PARTITION BY [VACCINE].[STUDENT_GU], [VACCINATION_DEF].[VACCINATION_NAME] ORDER BY [DOSAGE].[IMMUNIZATION_DATE]) [RN]
      FROM  
        	rev.[EPC_STU_IMM_VACCINE] AS [VACCINE]
            LEFT JOIN
            rev.[EPC_STU_IMM_DOSAGE] AS [DOSAGE]
            ON
            [VACCINE].[STU_IMM_VACCINE_GU] = [DOSAGE].[STU_IMM_VACCINE_GU]
            
			--THESE 2 JOINS ARE LOOKUPS
            LEFT  JOIN
            rev.[EPC_VACCINATION_DEF] AS [VACCINATION_DEF]
            ON
            [VACCINE].[VACCINATION_DEF_GU] = [VACCINATION_DEF].[VACCINATION_DEF_GU]
            
            LEFT  JOIN
            APS.LookupTable('K12.VaccinationInfo','Exemptions') AS [EXEMPTIONS]
            ON
            [VACCINE].[EXEMPT] = [EXEMPTIONS].[VALUE_CODE]
            
      ) AS [VAC_DOSAGES]      
	 /* 
	  INNER JOIN
	  rev.EPC_STU AS STU
	  ON
	  VAC_DOSAGES.STUDENT_GU = STU.STUDENT_GU
	*/        
GROUP BY
    --SIS_NUMBER,
	  [VAC_DOSAGES].[VACCINATION_NAME]
      ,[VAC_DOSAGES].[EXEMPT]
      ,[VAC_DOSAGES].[EXEMPT REASON]
      ,[VAC_DOSAGES].[EXEMPT_GRANTED]
	  ,[VAC_DOSAGES].[STUDENT_GU]
	  ,[STU_IMM_VACCINE_GU]

	  GO
