




EXECUTE AS LOGIN='QueryFileUser'
GO


SELECT
	[STUDENT_DETAILS].[SIS_NUMBER]
	,[STUDENT_DETAILS].[LAST_NAME]
	,[STUDENT_DETAILS].[FIRST_NAME]
	,[STUDENT_DETAILS].[MIDDLE_NAME]
	,DATEPART(YY,[STUDENT_DETAILS].[BIRTH_DATE]) AS [BIRTH_YEAR]
	,DATEPART(MM,[STUDENT_DETAILS].[BIRTH_DATE]) AS [BIRTH_MONTH]
	,[Organization].[ORGANIZATION_NAME] AS [SCHOOL_NAME]
	,[NAEP_SCHOOL_LIST].[NAEP ID]
	,[Grades].[VALUE_DESCRIPTION] AS [GRADE]
	,[STUDENT_DETAILS].[GENDER]
	,[STUDENT_DETAILS].[HISPANIC_INDICATOR]
	,[STUDENT_DETAILS].[RACE_1]
	,[STUDENT_DETAILS].[RACE_2]
	,[STUDENT_DETAILS].[RACE_3]
	,[STUDENT_DETAILS].[RACE_4]
	,[STUDENT_DETAILS].[RACE_5]
	,CASE 
		WHEN [STUDENT_DETAILS].[FRM_CODE] IS NULL THEN 'N'
		WHEN [STUDENT_DETAILS].[FRM_CODE] = '2' THEN 'F' 
		ELSE [STUDENT_DETAILS].[FRM_CODE] END AS [LUNCH_STATUS]
	,CASE WHEN [STUDENT_DETAILS].[ELL_STATUS] = 'Y' THEN 'Y'
		WHEN [STUDENT_DETAILS].[HAS_EVER_BEEN_ELL] = 'F' THEN 'F'
		ELSE 'N' END AS [ELL_STATUS]
	
	--,[STUDENT_DETAILS].[HAS_EVER_BEEN_ELL]
	,[STUDENT_DETAILS].[SPED_STATUS]
	
FROM
	APS.PrimaryEnrollmentsAsOf(GETDATE()) AS [Enrollments]
	
	INNER JOIN
	rev.EPC_STU_SCH_YR AS [StudentSchoolYear]
	ON
	[Enrollments].[STUDENT_SCHOOL_YEAR_GU] = [StudentSchoolYear].[STUDENT_SCHOOL_YEAR_GU]
	
	INNER JOIN 
	rev.REV_ORGANIZATION_YEAR AS [OrgYear] -- Links between School and Year
	ON 
	[StudentSchoolYear].[ORGANIZATION_YEAR_GU] = [OrgYear].[ORGANIZATION_YEAR_GU]
	
	INNER JOIN 
	rev.REV_ORGANIZATION AS [Organization] -- Contains the School Name
	ON 
	[OrgYear].[ORGANIZATION_GU] = [Organization].[ORGANIZATION_GU]
	
	INNER JOIN 
	rev.REV_YEAR AS [RevYear] -- Contains the School Year
	ON 
	[OrgYear].[YEAR_GU] = [RevYear].[YEAR_GU]
	
	LEFT OUTER JOIN
	APS.LookupTable('K12','Grade') AS [Grades]
	ON
	[StudentSchoolYear].[GRADE] = [Grades].[VALUE_CODE]
	
	INNER JOIN 
	rev.EPC_SCH AS [School] -- Contains the School Code / Number
	ON 
	[Organization].[ORGANIZATION_GU] = [School].[ORGANIZATION_GU]
	
	LEFT OUTER JOIN
	(
	SELECT 
		[SCHOOL_LIST].[School Number]
		,[SCHOOL_LIST].[NAEP ID] 
	FROM
		OPENROWSET (
			'Microsoft.ACE.OLEDB.12.0', 
			'Text;Database=\\SynTempSSIS\Files\TempQuery\;', 
			'SELECT * from NAEP_School_ID_Grade_List_with_CEP.csv'
		    ) AS [SCHOOL_LIST]
	) AS [NAEP_SCHOOL_LIST]
	ON
	[School].[SCHOOL_CODE] = CONVERT(VARCHAR,[NAEP_SCHOOL_LIST].[School Number])
	
	INNER JOIN
	(
	SELECT
		-- Basic Student Demographics
		[STUDENT].[STUDENT_GU]
		,[STUDENT].[SIS_NUMBER]
		,[STUDENT].[STATE_STUDENT_NUMBER]
		,[STUDENT].[LAST_NAME]
		,[STUDENT].[FIRST_NAME]
		,[STUDENT].[MIDDLE_NAME]
		,[STUDENT].[BIRTH_DATE]
		,[STUDENT].[GENDER]
		,[STUDENT].[CLASS_OF]	
		,[STUDENT].[HOME_ADDRESS] AS [HOME_ADDRESS]
		,[STUDENT].[HOME_ADDRESS_2] AS [HOME_ADDRESS_2]
		,[STUDENT].[HOME_CITY] AS [HOME_CITY]
		,[STUDENT].[HOME_STATE] AS [HOME_STATE]
		,[STUDENT].[HOME_ZIP] AS [HOME_ZIP]
		,[STUDENT].[MAIL_ADDRESS] AS [MAIL_ADDRESS]
		,[STUDENT].[MAIL_ADDRESS_2] AS [MAIL_ADDRESS_2]
		,[STUDENT].[MAIL_CITY] AS [MAIL_CITY]
		,[STUDENT].[MAIL_STATE] AS [MAIL_STATE]
		,[STUDENT].[MAIL_ZIP] AS [MAIL_ZIP]
		
		-- Format Primary Phone
		,'(' + left([PERSON].[PRIMARY_PHONE],3) + ')' 
		   + substring([PERSON].[PRIMARY_PHONE], 4,3)+ '-'
					   + RIGHT([PERSON].[PRIMARY_PHONE],4) AS [PRIMARY_PHONE]
	    
		-- Most Recent Lunch Status
		,[FRMHistory].[FRM_CODE]
	    
		-- Most Recent ELL Status
		,CASE WHEN [ELL_PGM].[PROGRAM_CODE] = '1' AND [ELL_PGM].[EXIT_DATE] IS NULL THEN 'Y' ELSE 'N' END AS [ELL_STATUS]
		,CASE WHEN [ELL_HISTORY].[ENTRY_DATE] IS NOT NULL THEN 'F' ELSE '' END AS [HAS_EVER_BEEN_ELL]
	    
		-- Current SPED or Gifted Status
		,[CurrentSPED].[PRIMARY_DISABILITY_CODE]
		-- SPED Not Gifted
		,CASE WHEN [CurrentSPED].[PRIMARY_DISABILITY_CODE] != 'GI' AND [CurrentSPED].[PRIMARY_DISABILITY_CODE] IS NOT NULL THEN 'Y' ELSE 'N' END AS [SPED_STATUS]
		,CASE WHEN [CurrentSPED].[PRIMARY_DISABILITY_CODE] = 'GI' OR [CurrentSPED].[SECONDARY_DISABILITY_CODE] = 'GI' THEN 'Y' ELSE 'N' END AS [GIFTED_STATUS]
	    
		-- Prefered Home Contact Language
		,CASE WHEN [Contact_Language].[VALUE_DESCRIPTION] IS NULL THEN [STUDENT].[HOME_LANGUAGE] ELSE [Contact_Language].[VALUE_DESCRIPTION] END AS [HOME_LANGUAGE2]
	    
		,[STUDENT].[HISPANIC_INDICATOR]
		,[ETHNIC_CODES].[RACE_1]
		,[ETHNIC_CODES].[RACE_2]
		,[ETHNIC_CODES].[RACE_3]
		,[ETHNIC_CODES].[RACE_4]
		,[ETHNIC_CODES].[RACE_5]
	    
	FROM
		APS.BasicStudent AS [STUDENT]
		
		INNER JOIN
		rev.REV_PERSON AS [PERSON]
		ON
		[STUDENT].[STUDENT_GU] = [PERSON].[PERSON_GU]
		
		LEFT JOIN
		(
		SELECT
				   *
		FROM
					REV.EP_STUDENT_SPECIAL_ED AS SPED
		WHERE
					NEXT_IEP_DATE IS NOT NULL
					AND (
								EXIT_DATE IS NULL 
								OR EXIT_DATE >= CONVERT(DATE, GETDATE())
								)
		) AS [CurrentSPED]
		ON
		[STUDENT].[STUDENT_GU] = [CurrentSPED].[STUDENT_GU]
	    
		LEFT JOIN
		rev.EPC_STU_PGM_ELL AS [ELL_PGM]
		ON
		[STUDENT].[STUDENT_GU] = [ELL_PGM].[STUDENT_GU] 
		
		LEFT JOIN
		(
		SELECT
			*
			,ROW_NUMBER() OVER (PARTITION BY [ELL_HISTORY].[STUDENT_GU] ORDER BY [ELL_HISTORY].[ENTRY_DATE] DESC) [RN]
		FROM
			rev.[EPC_STU_PGM_ELL_HIS] AS [ELL_HISTORY]
		) AS [ELL_HISTORY]
		ON
		[STUDENT].[STUDENT_GU] = [ELL_HISTORY].[STUDENT_GU] 
		AND [ELL_HISTORY].[RN] = 1
	    
		LEFT JOIN
		(
		SELECT
			  ROW_NUMBER() OVER (PARTITION BY [STU].[STUDENT_GU] order by [FRM].[ENTER_DATE],[STU].[STUDENT_GU]) [RN]
			, [STU].[STUDENT_GU]
			, [FRM].[FRM_CODE]
		FROM 
			rev.EPC_STU [STU]
			
			LEFT JOIN 
			rev.EPC_STU_PGM_FRM_HIS [FRM] 
			ON
			[FRM].[STUDENT_GU] = [STU].[STUDENT_GU]
			AND [FRM].[ENTER_DATE] IS NOT NULL 
			AND ([FRM].[EXIT_DATE] IS NOT NULL OR [FRM].[EXIT_DATE] > GETDATE())
		) AS [FRMHistory]
		ON
		[STUDENT].[STUDENT_GU] = [FRMHistory].[STUDENT_GU]
		AND [FRMHistory].[RN] = 1
		
		-- Get Home Contact Language
		LEFT JOIN
		APS.LookupTable ('K12', 'Language') AS [Contact_Language]	
		ON
		[ELL_PGM].[LANGUAGE_TO_HOME] = [Contact_Language].[VALUE_CODE]
		
		-- Get All Racial Codes
		LEFT JOIN
		(
		SELECT
			[ETHNIC_PIVOT].[PERSON_GU]
			,(SELECT [VALUE_DESCRIPTION] FROM APS.LookupTable ('Revelation', 'ETHNICITY') WHERE [VALUE_CODE] = [ETHNIC_PIVOT].[1]) AS [RACE_1]
			,(SELECT [VALUE_DESCRIPTION] FROM APS.LookupTable ('Revelation', 'ETHNICITY') WHERE [VALUE_CODE] = [ETHNIC_PIVOT].[2]) AS [RACE_2]
			,(SELECT [VALUE_DESCRIPTION] FROM APS.LookupTable ('Revelation', 'ETHNICITY') WHERE [VALUE_CODE] = [ETHNIC_PIVOT].[3]) AS [RACE_3]
			,(SELECT [VALUE_DESCRIPTION] FROM APS.LookupTable ('Revelation', 'ETHNICITY') WHERE [VALUE_CODE] = [ETHNIC_PIVOT].[4]) AS [RACE_4]
			,(SELECT [VALUE_DESCRIPTION] FROM APS.LookupTable ('Revelation', 'ETHNICITY') WHERE [VALUE_CODE] = [ETHNIC_PIVOT].[5]) AS [RACE_5]
		FROM
			(
			SELECT
				[SECONDARY_ETHNIC_CODES].[PERSON_GU]
				,[SECONDARY_ETHNIC_CODES].[ETHNIC_CODE]
				,ROW_NUMBER() OVER(PARTITION by [SECONDARY_ETHNIC_CODES].[PERSON_GU] order by [SECONDARY_ETHNIC_CODES].[ETHNIC_CODE]) [RN]
			FROM
				rev.REV_PERSON_SECONDRY_ETH_LST AS [SECONDARY_ETHNIC_CODES]
			) [PVT]
			PIVOT (MIN(ETHNIC_CODE) FOR [RN] IN ([1],[2],[3],[4],[5])) AS [ETHNIC_PIVOT]
		) AS [ETHNIC_CODES]
		ON
		[STUDENT].[STUDENT_GU] = [ETHNIC_CODES].[PERSON_GU]
		
	) AS [STUDENT_DETAILS]
	ON
	[Enrollments].[STUDENT_GU] = [STUDENT_DETAILS].[STUDENT_GU]
	
--WHERE
--	[Grades].[VALUE_DESCRIPTION] IN ('04','08','12')
--	AND [School].[SCHOOL_CODE] IN ('596','900')

-- Always conclude your statement with these last two lines so it reverts back
-- to your user
REVERT
GO